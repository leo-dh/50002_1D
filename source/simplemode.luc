module simplemode (
    input clk,  // clock
    input rst,  // reset
    
    input buttonadd,
    input buttonsub,
    input buttonmul,
    input buttondiv,
    input buttona,
    input buttonb,
    input buttonc,
    input aluout[16],
    
    output motherpos[16],
    output billypos[16],
    output a_out[16],
    output b_out[16],
    output c_out[16],
    output d_out[16],
    output operator_sig[6],
    output gameend,
    output alua[16],
    output alub[16],
    output alualufn[6]
    
  ) {
  .clk (clk){
    .rst (rst){
      fsm state = {IDLE,COUNTDOWN,GEN_QN,WAITINPUT,CHECKINPUT,CHECKINPUT2,CHECKMOTHERPOS,CHECKBILLYPOS,LOSE,WIN};
      dff timer[28];
      dff counter[2]; 
      counter ctr (#SIZE(1), #DIV(23));     
      pn_gen pn_gen;
      mother mother;
      billy billy;
      registers a(#BITS(16));
      registers b(#BITS(16));
      registers c(#BITS(16));
      registers temp(#BITS(16));
      registers operator(#BITS(6));
      registers divoutput(#BITS(16));
      
    }
  }
  generatequestion_simple questiongenerator;

  
  sig buttoninput[4];
  sig buttonoperatorinput[3];
  
  const ADD = 6b000000;
  const SUB = 6b000001;
  const MUL = 6b000010;
  const DIV = 6b100010;
  const CMPEQ = 6b110011;
  const WINCON = d8;
  
  always {
    gameend = 0;
    timer.d = timer.q + 1;
    pn_gen.seed = 1337;
    pn_gen.next = 0;
    questiongenerator.randomnumber = pn_gen.num;
    
    divoutput.writevalue = a.readvalue/b.readvalue;
    divoutput.writeenable = 1;
    
    alualufn = 6b0;
    alua = 16b0;
    alub = 16b0;
    
    mother.difficulty = b10;
    
    motherpos = mother.pos;
    billypos = billy.pos - d2;
    a_out = d20;
    b_out = a.readvalue;
    c_out = b.readvalue;
    d_out = c.readvalue;
    
    mother.writeenable = 0;
    billy.writeenable = 0;
    a.writeenable = 0;
    b.writeenable = 0;
    c.writeenable = 0;
    temp.writeenable = 0;
    
    mother.writevalue = mother.pos;
    billy.writevalue = billy.pos;
    a.writevalue = questiongenerator.a;
    b.writevalue = questiongenerator.b;
    c.writevalue = questiongenerator.c;
    temp.writevalue = 0;   
    
    operator_sig = operator.readvalue;
    operator.writeenable = 1;
    operator.writevalue = 6b111111;
    
    counter.d = counter.q;
    
    buttoninput = c{buttondiv, buttonmul, buttonsub, buttonadd};
    buttonoperatorinput = c{buttona,buttonb,buttonc};
    
    
    case (state.q){
      state.IDLE:
        timer.d = timer.q + 1;
        mother.writeenable = 1;
        mother.writevalue = d0;
        operator.writeenable = 1;
        operator.writevalue = 6b111111;
        mother.difficulty = b11; // keep mother timer at 0
        billy.writeenable = 1;
        billy.writevalue = d2; // billy position offset
        a_out = d11;
        b_out = d13;
        c_out = d5;
        d_out = d15;
        if ((|buttoninput) || (|buttonoperatorinput)){
          timer.d = 0;
          counter.d = d3;
          state.d = state.COUNTDOWN;
          
        }
      
      state.COUNTDOWN:
        timer.d = timer.q + 1;
        d_out = d20;
        case (counter.q){
          d3:
            a_out = counter.q;
            b_out = d20;
            c_out = d20;
                   
          d2:
            a_out = d20;
            b_out = counter.q;
            c_out = d20;
          
          d1:
            a_out = d20;
            b_out = d20;
            c_out = counter.q;
          
          default:
            a_out = d20;
            b_out = d20;
            c_out = d20;
        }
        
        mother.writeenable = 1;
        mother.writevalue = d0;
        operator.writeenable = 1;
        operator.writevalue = 6b111111;
        mother.difficulty = b11; // keep mother timer at 0
        if (timer.q == d50000000){
          timer.d = 0;
          counter.d = counter.q - 1;
        }
        if (counter.q == d0){
          counter.d = d0;
          state.d = state.GEN_QN;
        }
        
      
      state.GEN_QN:
        timer.d = 0;
        mother.writeenable = 1;  
        operator.writevalue = 6b111111;
        if (billy.pos >= d0){
          pn_gen.next = 1;
          a.writeenable = 1;
          b.writeenable = 1;
          c.writeenable = 1;
          temp.writeenable = 1;
          state.d = state.WAITINPUT;
        }
        if (billy.pos == WINCON){
          state.d = state.WIN;
        }
      
      state.WAITINPUT:
        timer.d  = timer.q + 1;
        operator.writevalue = 6b111111;
        alua = mother.pos;
        alub = billy.pos;
        alualufn = CMPEQ;
        if (aluout == 1){
          state.d = state.CHECKMOTHERPOS;
        }
        if (^buttoninput){
          operator.writeenable = 1;
          case (buttoninput){
            
            b0001:
              operator.writevalue = ADD;

            b0010:
              operator.writevalue = SUB;

            b0100:             
              operator.writevalue = MUL;
            
            b1000:              
              operator.writevalue = DIV;
          }
          timer.d = 0;
          state.d = state.CHECKINPUT;
        }
      
      state.CHECKINPUT:
        mother.writeenable = 1;
        timer.d = timer.q + 1;        
        alua = a.readvalue;
        alub = b.readvalue;
        alualufn = operator.readvalue;
        temp.writeenable = 1;
        case (operator.readvalue){
          DIV:
            temp.writevalue = divoutput.readvalue;
          
          default:
            temp.writevalue = aluout;
        }
        state.d = state.CHECKINPUT2;
            
      state.CHECKINPUT2:
        mother.writeenable = 1;
        timer.d = 0;
        alualufn = CMPEQ;
        alua = temp.readvalue;
        alub = c.readvalue;
        if (aluout == 1){
          state.d = state.CHECKBILLYPOS;
        }
        else{
          state.d = state.CHECKMOTHERPOS;
        }
        
      
      state.CHECKBILLYPOS:
        timer.d = 0;
        mother.writeenable = 1;
        
        counter.d = counter.q + 1;
        if (counter.q == d1){
          counter.d = 0;
          billy.writeenable = 1;
          billy.writevalue = billy.pos + 1;
        }
        alua = billy.pos;
        alub = WINCON;
        alualufn = CMPEQ;
        if (aluout == 1){
          state.d = state.WIN;
        }
        else{
          state.d = state.GEN_QN;
        }
        
        
      
      state.CHECKMOTHERPOS:
        timer.d = timer.q + 1;
        if (ctr.value == d1){
          a_out = d20;
          b_out = d20;
          c_out = d20;
          d_out = d20;
          operator_sig = 6b111111;
        }
        
        if (timer.q == d20000000){
          timer.d = 0;
          operator.writeenable = 1;
          operator.writevalue = 6b111111;
          state.d = state.WAITINPUT;
        }
        
        alua = mother.pos;
        alub = billy.pos;
        alualufn = CMPEQ;
        if (aluout == 1){
          state.d = state.LOSE;
        }
        
      
      state.WIN:
        timer.d = timer.q + 1;
        mother.writeenable = 1;
        a_out = d12;
        b_out = d13;
        c_out = d5;
        d_out = d5;
        
        // do something
        if (timer.q == d150000000){
          gameend = 1;
          state.d = state.IDLE;
        }
      
      state.LOSE:
        timer.d = timer.q + 1;
        mother.writeenable = 1;
        a_out = d10;
        b_out = d0;
        c_out = d5;
        d_out = d11;
        
        // do something
        if (timer.q == d150000000){
          gameend = 1;
          state.d = state.IDLE;
        }
      
      
      
      
    }
    
    
    
  }
}